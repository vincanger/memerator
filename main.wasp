app Memerator {
  wasp: {
    version: "^0.11.1"
  },
  title: "Memerator",
  client: {
    rootComponent: import { Layout } from "@client/Layout.jsx",
  },
  db: {
    system: PostgreSQL,
    prisma: {
      clientPreviewFeatures: ["extendedWhereUnique"]
    }
  },
  auth: {
    userEntity: User,
    methods: {
      usernameAndPassword: {}
    },
    onAuthFailedRedirectTo: "/login",
    onAuthSucceededRedirectTo: "/"
  },
  dependencies: [
    ("openai", "4.2.0"),
    ("axios", "^1.4.0"),
    ("react-icons", "4.10.1"),
  ]
}

route LoginRoute { path: "/login", to: LoginPage }
page LoginPage {
  component: import Login from "@client/pages/auth/Login.jsx"
}
route SignupRoute { path: "/signup", to: SignupPage }
page SignupPage {
  component: import Signup from "@client/pages/auth/Signup.jsx"
}

entity User {=psl
    id       Int    @id @default(autoincrement())
    username String @unique
    password String
    memes    Meme[]
    autoIdeas AutoMemeIdea[]
psl=}

entity Meme {=psl
    id         String @id @default(uuid())
    url        String 
    text0      String
    text1      String
    template   Template @relation(fields: [templateId], references: [id])
    templateId String
    user       User   @relation(fields: [userId], references: [id])
    userId     Int    
    autoMemeIdea AutoMemeIdea? @relation(fields: [autoMemeIdeaId], references: [id])
    autoMemeIdeaId String?
    createdAt  DateTime @default(now())
psl=}

entity Template {=psl
    id       String @id @unique
    name     String
    url      String
    width    Int
    height   Int
    boxCount Int
    memes    Meme[]
psl=}

entity AutoMemeIdea {=psl
    id       String    @id @default(uuid())
    topics   String
    audience String
    stopDate DateTime?
    status   String @default("pending")
    user     User   @relation(fields: [userId], references: [id])
    userId   Int
    memes    Meme[]
psl=}

action createMemeIdea {
  fn: import { createMemeIdea } from "@server/actions.js",
  entities: [Meme, Template, User]
}

action editMeme {
  fn: import { editMeme } from "@server/actions.js",
  entities: [Meme, Template]
}

action deleteMeme {
  fn: import { deleteMeme } from "@server/actions.js",
  entities: [Meme]
}

action createAutoMemeIdea {
  fn: import { createAutoMemeIdea } from "@server/actions.js",
  entities: [AutoMemeIdea, User]
}

action deleteAutoMemeIdea {
  fn: import { deleteAutoMemeIdea } from "@server/actions.js",
  entities: [AutoMemeIdea]
}

query getAllMemeIdeas {
  fn: import { getAllMemeIdeas } from "@server/queries.js",
  entities: [Meme]
}

query getMeme {
  fn: import { getMeme } from "@server/queries.js",
  entities: [Meme]
}

query getAutoMemeIdeas {
  fn: import { getAutoMemeIdeas } from "@server/queries.js",
  entities: [AutoMemeIdea]
}

query getAutoMemes {
  fn: import { getAutoMemes } from "@server/queries.js",
  entities: [AutoMemeIdea]
}

query getMemeTemplates {
  fn: import { getMemeTemplates } from "@server/queries.js",
  entities: [Template]
}

route MemeIdeaRoute { path: "/meme-idea/:id", to: MemeIdeaPage }
page MemeIdeaPage {
  component: import { MemeIdeaPage } from "@client/pages/MemeIdea.jsx",
  authRequired: true
}

route MemeRoute { path: "/meme/:memeId", to: MemePage }
page MemePage {
  component: import { MemePage } from "@client/pages/Meme.jsx",
  authRequired: false
}

route HomePageRoute { path: "/", to: HomePage }
page HomePage {
  component: import { HomePage } from "@client/pages/Home.jsx",
  authRequired: true
}

route AutoMemeRoute { path: "/auto-meme", to: AutoMemePage }
page AutoMemePage {
  component: import { AutoMemePage } from "@client/pages/AutoMeme.jsx",
  authRequired: true
}

route AutoMemeIdeaRoute { path: "/auto-meme/:autoMemeIdeaId", to: AutoMemeIdeaPage }
page AutoMemeIdeaPage {
  component: import { AutoMemeIdeaPage } from "@client/pages/AutoMemeIdea.jsx",
  authRequired: true
}

job storeMemeTemplates {
  executor: PgBoss,
  perform: {
    fn: import { getMemeTemplates } from "@server/workers.js",
  },
  schedule: {
    // daily at 7 a.m.
    cron: "0 7 * * *" 
  },
  entities: [Template],
}

job generateAutoMemes {
  executor: PgBoss,
  perform: {
    fn: import { runAutoMemes } from "@server/workers.js",
  },
  schedule: {
    // every hour
    cron: "1 * * * *"  
  },
  entities: [Meme, Template, AutoMemeIdea],
}
